plugins {
  id 'org.springframework.boot' version '2.3.5.RELEASE' apply false
  id 'io.spring.dependency-management' version '1.0.10.RELEASE' apply false
  id 'com.github.ben-manes.versions' version '0.33.0'
  id 'se.patrikerdes.use-latest-versions' version '0.2.15'
  id 'org.sonatype.gradle.plugins.scan' version '1.2.4'
}
allprojects {
  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
  }
}
subprojects {
  apply plugin: 'java'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.springframework.boot'

  group = 'com.fhnw.eaf'
  version = '0.0.1-SNAPSHOT'
  sourceCompatibility = 11
  targetCompatibility = 11

  //managed dependency versions
  dependencyManagement {
    dependencies {
      dependency 'org.apache.tika:tika-parsers:1.24.1'
      dependency 'org.flywaydb:flyway-core:6.5.3'
      dependencySet(group: 'org.jooq', version: '3.13.2') {
        entry 'jooq'
        entry 'jooq-codegen'
      }
      dependency 'org.mariadb.jdbc:mariadb-java-client:2.6.2'
      dependency 'org.mapstruct:mapstruct:1.4.1.Final'
      dependency 'org.mapstruct:mapstruct-processor:1.4.1.Final'
      dependency 'org.springdoc:springdoc-openapi-ui:1.4.8'
    }
  }
  dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test')
  }
  test {
    useJUnitPlatform()
  }
  tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file("$buildDir/generated")
  }

  sourceSets {
    main {
      java {
        srcDirs += ["$buildDir/generated"]
      }
    }
  }

}
